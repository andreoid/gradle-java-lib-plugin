plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.9.2'
    id 'com.jfrog.bintray' version '1.5'
    id 'net.researchgate.release' version '2.3.4'
    id 'ru.vyarus.quality' version '1.2.0'
    id 'ru.vyarus.java-lib' version '1.0.1'
    id 'ru.vyarus.github-info' version '1.0.0'
    id "com.github.ben-manes.versions" version "0.11.3"
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

wrapper {
    gradleVersion = 2.9
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

repositories { jcenter() }
dependencies {
    compile 'ru.vyarus:gradle-pom-plugin:1.0.1'

    testCompile gradleTestKit()
    testRuntime files(createClasspathManifest)
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

group = 'ru.vyarus'
description = 'Gradle Java-lib plugin'

github {
    user 'xvik'
    license 'MIT'
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email "vyarus@gmail.com"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : 'USER'
    key = project.hasProperty('bintrayKey') ? bintrayKey : 'KEY'
    publications = ['maven']
    dryRun = false
    publish = true
    pkg {
        repo = 'xvik'
        name = project.name
        desc = project.description
        labels = ['gradle', 'java', 'groovy', 'lib']
        publicDownloadNumbers = true
        version {
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? gpgPassphrase : ''
            }
            mavenCentralSync {
                sync = true
                user = project.hasProperty('sonatypeUser') ? sonatypeUser : 'USER'
                password = project.hasProperty('sonatypePassword') ? sonatypePassword : 'PASSWORD'
            }
        }
    }
}

pluginBundle {
    description = 'Common gradle configuration for Java or Groovy library'
    tags = ['java', 'groovy', 'lib']

    plugins {
        javaLibPlugin {
            id = 'ru.vyarus.java-lib'
            displayName = project.description
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
// HOTFIX: plugin-publish don't see properties in global gradle.properties
System.setProperty('gradle.publish.key', project.properties['gradle.publish.key'] ?: '')
System.setProperty('gradle.publish.secret', project.properties['gradle.publish.secret'] ?: '')

afterReleaseBuild {
    dependsOn = [bintrayUpload, publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events "skipped", "failed", "standardError"
        exceptionFormat "full"
    }
}

dependencyUpdates {
    doFirst {
        project.group = 'foo'
    }
    revision = 'release'
}
